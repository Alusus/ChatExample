اشمل "مـتم/نـص"؛
اشمل "مـتم/مـنشئ_نص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/وقـت"؛
اشمل "مـتم/نـظام"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/أخطاء"؛
اشمل "مغلفة"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Json"، "جـيسون.أسس")؛
مـحا.اشمل_ملف("Alusus/Http"، "بـننف.أسس")؛
مـحا.اشمل_ملف("Alusus/Rows"، { "صـفوف.أسس"، "مـشغلات/بـوستغرس.أسس" })؛
مـحا.اشمل_ملف("Alusus/WebPlatform"، "مـنصة_ويب.أسس")؛
مـحا.اشمل_ملف("Alusus/Promises"، "مـؤجلات.أسس")؛
مـحا.اشمل_ملف("Alusus/Nashir"، { "نـاشر.أسس"، "مـشغلات_نشر/الـأسس_نت.أسس" })؛
مـحا.اشمل_ملف("Alusus/ProgArg"، "بـروجارج.أسس")؛
استخدم مـتم؛
استخدم مـنصة_ويب؛
استخدم مـؤجلات؛

//==============================================================================
// قاعدة البيانات

ماكرو متغير_بيئي_أو_قيمة_مبدئية[اسم_المتغير، القيمة_الافتراضية] مـؤشر_محارف().{
    هذا = نـظام.هات_متغير_محيطي(اسم_المتغير)؛
    إذا هذا == 0 هذا = القيمة_الافتراضية؛
}

@جدول["rooms"، 1]
صنف جـدول_غرفة {
    صـفوف.عرف_أساسيات_الجدول[]؛

    @مـحارف_مرنة["256"]
    @فهرس_رئيسي
    @حقل["name"]
    عرف الاسم: نـص؛

    @مـحارف_مرنة["1024"]
    @إلزامي
    @حقل["password"]
    عرف كلمة_المرور: نـص؛
}

@جدول["users"، 1]
صنف جـدول_مستخدم {
    صـفوف.عرف_أساسيات_الجدول[]؛

    @مـحارف_مرنة["256"]
    @فهرس_رئيسي
    @حقل["username"]
    عرف اسم_المستخدم: نـص؛

    @مـحارف_مرنة["1024"]
    @إلزامي
    @حقل["password"]
    عرف كلمة_المرور: نـص؛
}

@جدول["messages"، 2]
صنف جـدول_رسالة {
    صـفوف.عرف_أساسيات_الجدول[]؛

    @مفتاح_أجنبي["rooms"، "name"]
    @مـحارف_مرنة["256"]
    @إلزامي
    @حقل["room_name"]
    عرف اسم_الغرفة: نـص؛

    @مفتاح_أجنبي["users"، "username"]
    @مـحارف_مرنة["256"]
    @إلزامي
    @حقل["username"]
    عرف اسم_المستخدم: نـص؛

    @مـحارف_مرنة["1024"]
    @إلزامي
    @حقل["message"]
    عرف الرسالة: نـص؛

    @صـحيح_كبير
    @إلزامي
    @حقل["creation_date"]
    عرف تاريخ_الإنشاء: صـحيح[64]؛

    @مواكبة[1، 2، { جـدول_غرفة: 1؛ جـدول_مستخدم: 1 }]
    دالة الترحيل_إلى_الإصدار2(قاعدة: سند[صـفوف.قـاعدة_بيانات]): سـندنا[خـطأ] {
        قاعدة.نفذ("alter table messages add column room_name varchar(256) not null references rooms(name)")؛
        قاعدة.نفذ("alter table messages add column username varchar(256) not null references users(username)")؛
        أرجع سـندنا[خـطأ]()؛
    }
}

عرف قاعدة: صـفوف.قـاعدة_بيانات؛
عرف مخطط_قاعدة_البيانات: {
    جـدول_غرفة،
    جـدول_مستخدم،
    جـدول_رسالة
}؛

دالة تهيئة_قاعدة_البيانات {
    قاعدة.هيئ(مغلفة(م: سند[سـندنا[صـفوف.مـشغل]]) {
        م = هات_مشغل_قاعدة_البيانات()؛
    })؛
    قاعدة.مهيكل[مخطط_قاعدة_البيانات].واكب()؛
}

دالة هات_مشغل_قاعدة_البيانات(): سـندنا[صـفوف.مـشغل] {
    عرف مشغل: سـندنا[صـفوف.مـشغل] = صـفوف.مـشغل_بوستغرس(صـفوف.مـعطيات_الاتصال().{
        اسم_قاعدة_البيانات = نـص(متغير_بيئي_أو_قيمة_مبدئية["DB_NAME"، "alusus"]).شذب()؛
        اسم_المستخدم = نـص(متغير_بيئي_أو_قيمة_مبدئية["DB_USERNAME"، "alusus"]).شذب()؛
        كلمة_السر = نـص(متغير_بيئي_أو_قيمة_مبدئية["DB_PASSWORD"، "alusus"]).شذب()؛
        عنوان_الخادم = نـص(متغير_بيئي_أو_قيمة_مبدئية["DB_HOST"، "0.0.0.0"]).شذب()؛
        المنفذ = نـص.اقرأ_صحيح(متغير_بيئي_أو_قيمة_مبدئية["DB_PORT"، "5432"])؛
    })؛
    إذا !مشغل.أمتصل() {
        نـظام.فشل(1، نـص("خطأ في الاتصال بقاعدة البيانات: ") + مشغل.هات_آخر_خطأ())؛
    }
    ارجع مشغل؛
}

//==============================================================================
// الخلفية

عرف أقصى_عدد_للرسائل: 12؛

@منفذ_بياني["POST"، "/validate-creds"]
دالة التحقق_من_بيانات_الدخول (اتصال: مؤشر[بـننف.اتـصال]) {
    عرف بيانات_النشر: مصفوفة[محرف، 1024]؛
    عرف حجم_بيانات_النشر: صـحيح = بـننف.اقرأ(اتصال، بيانات_النشر~مؤشر، بيانات_النشر~حجم)؛
    عرف جيسون: جـيسون = نـص(بيانات_النشر~مؤشر، حجم_بيانات_النشر)؛
    عرف اسم_الغرفة: نـص = جيسون("roomName")~مثل[نـص].شذب()؛
    عرف كلمة_مرور_الغرفة: نـص = جيسون("roomPassword")؛
    عرف اسم_المستخدم: نـص = جيسون("userName")~مثل[نـص].شذب()؛
    عرف كلمة_مرور_المستخدم: نـص = جيسون("userPassword")؛
    // التحقق من المستخدم أو إنشاؤه
    عرف المستخدمون: مـصفوفة[سـندنا[جـدول_مستخدم]] = قاعدة.من[جـدول_مستخدم].حيثما[اسم_المستخدم == اسم_المستخدم].اجلب()؛
    إذا المستخدمون.هات_الطول() == 0 {
        قاعدة.احفظ[جـدول_مستخدم](جـدول_مستخدم()~استخدم_في (ذات) بلا_حقن {
            ذات.اسم_المستخدم = اسم_المستخدم؛
            ذات.كلمة_المرور = كلمة_مرور_المستخدم؛
        })؛
    } وإلا إذا المستخدمون(0).كلمة_المرور != كلمة_مرور_المستخدم {
        بـننف.اطبع(اتصال، "HTTP/1.1 401 Unauthorized\r\n\r\n")؛
        ارجع؛
    }
    // التحقق من الغرفة أو إنشاؤها
    عرف الغرف: مـصفوفة[سـندنا[جـدول_غرفة]] = قاعدة.من[جـدول_غرفة].حيثما[الاسم == اسم_الغرفة].اجلب()؛
    إذا الغرف.هات_الطول() == 0 {
        قاعدة.احفظ[جـدول_غرفة](جـدول_غرفة().{
            الاسم = اسم_الغرفة؛
            كلمة_المرور = كلمة_مرور_الغرفة؛
        })؛
    } وإلا إذا الغرف(0).كلمة_المرور != كلمة_مرور_الغرفة {
        بـننف.اطبع(اتصال، "HTTP/1.1 401 Unauthorized\r\n\r\n")؛
        ارجع؛
    }
    // ناجح
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n\r\n")؛
}

@منفذ_بياني["POST"، "/add-message"]
دالة نشر_الرسائل (اتصال: مؤشر[بـننف.اتـصال]) {
    عرف بيانات_النشر: مصفوفة[محرف، 4096]؛
    عرف حجم_بيانات_النشر: صـحيح = بـننف.اقرأ(اتصال، بيانات_النشر~مؤشر، بيانات_النشر~حجم)؛
    عرف جيسون: جـيسون = نـص(بيانات_النشر~مؤشر، حجم_بيانات_النشر)؛
    إذا ليس التحقق_من_بيانات_الدخول(جيسون) {
        بـننف.اطبع(اتصال، "HTTP/1.1 401 Unauthorized\r\n\r\n")؛
        ارجع؛
    }
    عرف رسالة: جـدول_رسالة؛
    رسالة.اسم_المستخدم = جيسون("userName")~مثل[نـص].شذب()؛
    رسالة.اسم_الغرفة = جيسون("roomName")~مثل[نـص].شذب()؛
    رسالة.الرسالة = جيسون("message")؛
    رسالة.تاريخ_الإنشاء = وقـت.هات_الختم_الزمني(0)؛
    قاعدة.احفظ[جـدول_رسالة](رسالة)؛
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n\r\n")؛
}

@منفذ_بياني["POST"، "/get-messages"]
دالة الحصول_على_الرسائل (اتصال: مؤشر[بـننف.اتـصال]) {
    عرف بيانات_النشر: مصفوفة[محرف، 4096]؛
    عرف حجم_بيانات_النشر: صـحيح = بـننف.اقرأ(اتصال، بيانات_النشر~مؤشر، بيانات_النشر~حجم)؛
    عرف جيسون: جـيسون = نـص(بيانات_النشر~مؤشر، حجم_بيانات_النشر)؛
    إذا ليس التحقق_من_بيانات_الدخول(جيسون) {
        بـننف.اطبع(اتصال، "HTTP/1.1 401 Unauthorized\r\n\r\n")؛
        ارجع؛
    }
    // جلب الرسائل من قاعدة البيانات
    عرف الرسائل: مـصفوفة[سـندنا[جـدول_رسالة]] = قاعدة
        .من[جـدول_رسالة]
        .حيثما[اسم_الغرفة == جيسون("roomName")~مثل[نـص].شذب()]
        .رتب[-تاريخ_الإنشاء]
        .اجلب()
        .اجتزئ(0، أقصى_عدد_للرسائل)؛
    عرف رد: مـنشئ_نص(1024 * 10، 1024 * 5)؛
    عرف ط: صـحيح؛
    لكل ط = الرسائل.هات_الطول() - 1، ط >= 0، --ط {
        إذا ط != الرسائل.هات_الطول() - 1 رد += "\n"؛
        رد.املأ("- %s: %s"، الرسائل(ط).اسم_المستخدم.صوان، الرسائل(ط).الرسالة.صوان)؛
    }
    // حذف الرسائل الأقدم
    إذا الرسائل.هات_الطول() > 0 {
        قاعدة.من[جـدول_رسالة]
            .حيثما[اسم_الغرفة == جيسون("roomName")~مثل[نـص].شذب() و
                تاريخ_الإنشاء < الرسائل(الرسائل.هات_الطول() - 1).تاريخ_الإنشاء]
            .احذف()؛
    }
    // إرسال الرد
    بـننف.اطبع(اتصال، "HTTP/1.1 200 Ok\r\n")؛
    بـننف.اطبع(اتصال، "Content-Type: text/plain\r\n")؛
    بـننف.اطبع(اتصال، "Cache-Control: no-cache\r\n")؛
    بـننف.اطبع(اتصال، "Content-Length: %d\r\n\r\n"، رد.هات_الطول())؛
    بـننف.اطبع(اتصال، رد)؛
}

دالة التحقق_من_بيانات_الدخول (جيسون: جـيسون): ثـنائي {
    عرف اسم_الغرفة: نـص = جيسون("roomName")~مثل[نـص].شذب()؛
    عرف كلمة_مرور_الغرفة: نـص = جيسون("roomPassword")؛
    عرف اسم_المستخدم: نـص = جيسون("userName")~مثل[نـص].شذب()؛
    عرف كلمة_مرور_المستخدم: نـص = جيسون("userPassword")؛
    عرف المستخدمون: مـصفوفة[سـندنا[جـدول_مستخدم]] = قاعدة.من[جـدول_مستخدم].حيثما[اسم_المستخدم == اسم_المستخدم].اجلب()؛
    إذا المستخدمون.هات_الطول() == 0 أو المستخدمون(0).كلمة_المرور != كلمة_مرور_المستخدم {
        ارجع 0؛
    }
    عرف الغرف: مـصفوفة[سـندنا[جـدول_غرفة]] = قاعدة.من[جـدول_غرفة].حيثما[الاسم == اسم_الغرفة].اجلب()؛
    إذا الغرف.هات_الطول() == 0 أو الغرف(0).كلمة_المرور != كلمة_مرور_الغرفة {
        ارجع 0؛
    }
    ارجع 1؛
}

//==============================================================================
// مكونات الواجهة الأمامية

صنف تـرويسة {
    @حقنة عرف مركب: مـركب؛

    عملية هذا~هيئ() {
        هذا.المشهد = صـندوق({}).{
            الطراز.{
                الحشوة = مـسافة4.نقاط(4)؛
                سمك_الإطار = مـسافة4.نقاط(0، 0، 1.5، 0)؛
                طراز_الإطار = طـراز_إطار._مستمر_؛
                لون_الإطار = لـون("000")؛
                ملء_السطر = مـلء_سطر._بداية_؛
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._عمود_؛
                الخلفية = خـلفية(لـون("fff"))؛
            }؛
            أضف_فروع({
                كـتابة(نـص("مثال الدردشة")).{
                    الطراز.حجم_الخط = مـسافة.نقاط(18.0)؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[تـرويسة] {
        ارجع سـندنا[تـرويسة].أنشئ()؛
    }
}

صنف مـدخل_نصي {
    @حقنة عرف مركب: مـركب؛

    عرف عند_إدخال_جديد: مغلفة (نـص)؛
    عرف مدخل_نص: سـندنا[مـدخل_نص]؛

    عملية هذا~هيئ() {
        عرف ذات: سند[هذا_الصنف](هذا)؛

        هذا.المشهد = صـندوق({}).{
            الطراز.{
                الإظهار = إظـهار._مرن_؛
                النسق = نـسق._سطر_؛
                ملء_السطر = مـلء_سطر._مسافة_بينية_؛
            }؛
            أضف_فروع({
                مـدخل_نص().{
                    ذات.مدخل_نص = هذا؛
                    الطراز.{
                        المرونة = مـرونة(1)؛
                        الهامش = مـسافة4.بكسلات(10)؛
                        الطول = مـسافة.بكسلات(50)؛
                        حجم_الخط = مـسافة.نقاط(12.0)؛
                        طراز_الإطار = طـراز_إطار._مستمر_؛
                        لون_الإطار = لـون("000")؛
                        سمك_الإطار = مـسافة4.بكسلات(1.5)؛
                    }؛
                    عند_انتهاء_الكبسة.اربط(مغلفة (عنصر_واجهة: سند[مـدخل_نص]، حمولة: سند[نـص]) {
                        إذا حمولة == "Shift+Enter" {
                            عرف بيانات_جديدة: نـص = عنصر_واجهة.هات_النص().شذب()؛
                            عنصر_واجهة.حدد_النص(نـص())؛
                            إذا ليس ذات.عند_إدخال_جديد.أهو_عدم() ذات.عند_إدخال_جديد(بيانات_جديدة)؛
                        }
                    })؛
                }،
                الـزر(نـص("أرسل")).{
                    الطراز.{
                        الطول = مـسافة.بكسلات(50)؛
                        العرض = مـسافة.بكسلات(50)؛
                        حجم_الخط = مـسافة.بكسلات(16.0)؛
                        ملء_السطر = مـلء_سطر._وسط_؛
                        الهامش = مـسافة4.بكسلات(10، 0، 10، 10)؛
                    }؛
                    عند_الضغط.اربط(مغلفة (عنصر_واجهة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                        عرف بيانات_جديدة: نـص = ذات.مدخل_نص.هات_النص().شذب()؛
                        ذات.مدخل_نص.حدد_النص(نـص())؛
                        إذا ليس ذات.عند_إدخال_جديد.أهو_عدم() ذات.عند_إدخال_جديد(بيانات_جديدة)؛
                    })؛
                }
            })؛
        }؛
    }

    عملية هذا_الصنف(): سـندنا[مـدخل_نصي] {
        ارجع سـندنا[مـدخل_نصي].أنشئ()؛
    }
}

//==============================================================================
// صفحات الواجهة الأمامية

عرف اسم_المستخدم: نـص؛
عرف كلمة_مرور_المستخدم: نـص؛
عرف اسم_الغرفة: نـص؛
عرف كلمة_مرور_الغرفة: نـص؛

دالة تسجيل_الدخول {
    عرف نجاح: سـندنا[مـؤجلة[صـحيح]] = مـؤجلة[صـحيح].أنشئ()؛
    عرف إشعار: سـندنا[كـتابة]؛
    عرف اسم_الغرفة: سـندنا[مـدخل]؛
    عرف كلمة_مرور_الغرفة: سـندنا[مـدخل]؛
    عرف اسم_المستخدم: سـندنا[مـدخل]؛
    عرف كلمة_مرور_المستخدم: سـندنا[مـدخل]؛

    نـافذة.النموذج.حدد_المشهد(صـندوق().{
        الطراز.{
            الطول = مـسافة.مئوي(100)؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
            ملء_السطر = مـلء_سطر._وسط_؛
            المحاذاة = مـحاذاة._وسط_؛
        }؛
        أضف_فروع({
            كـتابة().{
                إشعار = هذا؛
                الطراز.{
                    الهامش = مـسافة4.بكسلات(10)؛
                    لون_الخط = لـون("f00")؛
                }؛
            }،
            مـدخل().{
                اسم_المستخدم = هذا؛
                الطراز.الهامش = مـسافة4.بكسلات(10)؛
                قيمة_وصفية = نـص("اسم المستخدم")؛
            }،
            مـدخل().{
                كلمة_مرور_المستخدم = هذا؛
                الطراز.الهامش = مـسافة4.بكسلات(10)؛
                قيمة_وصفية = نـص("كلمة المرور")؛
            }،
            مـدخل().{
                اسم_الغرفة = هذا؛
                الطراز.الهامش = مـسافة4.بكسلات(10)؛
                قيمة_وصفية = نـص("اسم الغرفة")؛
            }،
            مـدخل().{
                كلمة_مرور_الغرفة = هذا؛
                الطراز.الهامش = مـسافة4.بكسلات(10)؛
                قيمة_وصفية = نـص("سر الغرفة")؛
            }،
            الـزر(نـص("إرسال")).{
                الطراز.الهامش = مـسافة4.بكسلات(10)؛
                عند_الضغط.اربط(مغلفة (عنصر_واجهة: سند[ودجـة]، حمولة: سند[صـحيح]) {
                    إشعار.حدد_النص(نـص())؛
                    عرف جيسون: مـنشئ_نص[مـكون_منشئ_نص_جيسون](1024، 1025)؛
                    جيسون.املأ(
                        "{ \"userName\": %js, \"userPassword\": %js, \"roomName\": %js, \"roomPassword\": %js }"،
                        اسم_المستخدم.هات_النص().شذب()،
                        كلمة_مرور_المستخدم.هات_النص()،
                        اسم_الغرفة.هات_النص().شذب()،
                        كلمة_مرور_الغرفة.هات_النص()
                    )؛
                    أرسل_نداء(
                        "POST"، "/validate-creds"، "Content-Type: application/text"، جيسون، 10000،
                        مغلفة (جيسون: جـيسون) {
                            عرف حالة: صـحيح = جيسون("eventData")("status")؛
                            إذا حالة == 200 {
                                الـجذر.اسم_المستخدم = اسم_المستخدم.هات_النص().شذب()؛
                                الـجذر.كلمة_مرور_المستخدم = كلمة_مرور_المستخدم.هات_النص()؛
                                الـجذر.اسم_الغرفة = اسم_الغرفة.هات_النص().شذب()؛
                                الـجذر.كلمة_مرور_الغرفة = كلمة_مرور_الغرفة.هات_النص()؛
                                نجاح.قرر(0)؛
                            } وإلا {
                                إشعار.حدد_النص(نـص("فشل تسجيل الدخول!"))؛
                            }
                        }
                    )؛
                })؛
            }
        })؛
    })؛

    انتظر(نجاح)؛
}

@منفذ_مرئي["/"]
@عنوان["مثال الدردشة"]
دالة رئيسية {
    عرف عند_الجلب: مغلفة (جيسون: جـيسون)؛

    نـافذة.النموذج.الطراز.{
        الحشوة = مـسافة4.نقاط(0)؛
        الهامش = مـسافة4.نقاط(0)؛
        الاتجاه = اتـجاه._من_اليمين_؛
    }؛

    تسجيل_الدخول()؛

    عرف جيسون_جلب_الرسائل: نـص = مـنشئ_نص[مـكون_منشئ_نص_جيسون](1024، 1025).{
        املأ(
            ‏"{ \"userName\": %js, \"userPassword\": %js, \"roomName\": %js, \"roomPassword\": %js }"،
            اسم_المستخدم، كلمة_مرور_المستخدم، اسم_الغرفة، كلمة_مرور_الغرفة
        )؛
    }؛

    نـافذة.النموذج.حدد_المشهد(صـندوق({}).{
        الطراز.{
            الطول = مـسافة.مئوي(100)؛
            ملء_السطر = مـلء_سطر._مسافة_بينية_؛
            الإظهار = إظـهار._مرن_؛
            النسق = نـسق._عمود_؛
            الخلفية = خـلفية(لـون("aaa"))؛
        }؛
        أضف_فروع({
            تـرويسة()،
            كـتابة(نـص()).{
                الطراز.{
                    المرونة = مـرونة(1)؛
                    العرض = مـسافة.مئوي(100) - مـسافة.بكسلات(20)؛
                    الهامش = مـسافة4.بكسلات(10، 10، 0، 10)؛
                    حجم_الخط = مـسافة.نقاط(20.0)؛
                    طراز_الإطار = طـراز_إطار._مستمر_؛
                    لون_الإطار = لـون("000")؛
                    سمك_الإطار = مـسافة4.بكسلات(1.5)؛
                    الخلفية = خـلفية(لـون("fff"))؛
                }؛
                عند_الجلب = مغلفة (جيسون: جـيسون) {
                    عرف حالة: صـحيح[64] = جيسون("eventData")("status")؛
                    إذا حالة >= 200 و حالة < 300 {
                        عرف بيانات: نـص = جيسون("eventData")("body")؛
                        إذا هذا.هات_النص() != بيانات {
                            هذا.حدد_النص(بيانات)؛
                        }
                    }
                }؛
            }،
            مـدخل_نصي().{
                عند_إدخال_جديد = مغلفة (بيانات_جديدة: نـص) {
                    عرف جيسون_نشر_الرسالة: مـنشئ_نص[مـكون_منشئ_نص_جيسون](1024، 1025)؛
                    جيسون_نشر_الرسالة.املأ(
                        "{ \"userName\": %js, \"userPassword\": %js, \"roomName\": %js, \"roomPassword\": %js, \"message\": %js }"،
                        اسم_المستخدم، كلمة_مرور_المستخدم، اسم_الغرفة، كلمة_مرور_الغرفة، بيانات_جديدة
                    )؛
                    أرسل_نداء(
                        ‏"POST"، "/add-message"، "Content-Type: application/json"، جيسون_نشر_الرسالة، 10000،
                        مغلفة (جـيسون) {
                            أرسل_نداء("POST"، "/get-messages"، "Content-Type: application/json"، جيسون_جلب_الرسائل، 500، عند_الجلب)؛
                        }
                    )؛
                }؛
            }
        })؛
    })؛

    ابدأ_المؤقت_المتكرر(500000، مغلفة (جيسون: جـيسون) {
        أرسل_نداء("POST"، "/get-messages"، "Content-Type: application/json"، جيسون_جلب_الرسائل، 500، عند_الجلب)؛
    })؛
    أرسل_نداء("POST"، "/get-messages"، "Content-Type: application/json"، جيسون_جلب_الرسائل، 500، عند_الجلب)؛

    نفذ_حلقة_معالجة_الأحداث()؛
}

//==============================================================================
// نقطة الدخول

دالة تهيئة_الخادم {
    تهيئة_قاعدة_البيانات()؛
}

نـاشر.اضبط[نـاشر.إعـدادات().{
    اسم_المشروع = "chat-example-arabic"؛
    رقم_الإصدار = "v1"؛
    منفذ_الخادم = 8000؛
    مهيئ~عطل_التتبع = تهيئة_الخادم~شبم؛
    الاعتماديات.أضف(صـفوف.مـشغل_بوستغرس.هات_اعتماديات_البناء())؛
    مشغل_النشر = نـاشر.مـشغل_نشر_الأسس_نت()؛
}]؛
بـروجارج.حلل(2)؛
